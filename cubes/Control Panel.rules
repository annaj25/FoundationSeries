
[]=If(ELISANC('Version','All Reporting Versions',!Version) =1,STET,Continue);

# Prevent Starting Forecast Month from being set for the version called Actuals
[ Version:'Actuals', ControlPanelParameter:'Starting Forecast Month', ControlPanelMeasure:'Value'] = S: '';
# Description for Setting the Starting Forecast Month
[ ControlPanelParameter:'Starting Forecast Month', ControlPanelMeasure:'Numeric Value'] = N:
	NUMBR(ATTRS('Period',DB( 'Control Panel', !Version, !Control Panel Parameter, 'Value' ),'Numeric Month'));
# Converts Starting Forecast Month into a number value
[ ControlPanelParameter:'Starting Forecast Month', ControlPanelMeasure:'Description' ] = S: 'Select the Month that the Forecast starts.';

# Prevent Starting Forecast Year from being set for the version called Actuals
[ Version:'Actuals', ControlPanelParameter:'Starting Forecast Year', ControlPanelMeasure:'Value'] = S: '';
# Description for Setting the Starting Forecast Year
[ ControlPanelParameter:'Starting Forecast Year', ControlPanelMeasure:'Description' ] = S: 'Select the Year that the Forecast starts.';
# Converts Starting Forecast Year into a number value
[ ControlPanelParameter:'Starting Forecast Year', ControlPanelMeasure:'Numeric Value' ] = N: Numbr ( DB('Control Panel', !Version, 'Starting Forecast Year', 'Value')  );

['Starting Forecast Period','Value'] = S: DB('Control Panel', !Version, 'Starting Forecast Year', 'Value') | '-' | DB('Control Panel', !Version, 'Starting Forecast Month', 'Value');
['Starting Forecast Period', 'Numeric Value'] = ATTRN( 'Period', DB('Control Panel',!Version,'Starting Forecast Period','Value'), 'Month Sequence' );
['Last Actual Period','Value'] = S: ATTRS('Period',DB('Control Panel', !Version, 'Starting Forecast Period', 'Value'),'Prior Period');

# Prevent Last Actual Month from being calculated for the version called Actuals
[ Version:'Actuals', ControlPanelParameter:'Last Actual Month', ControlPanelMeasure:'Value'] = S: '';
# Calculates Last Actual Month
[ControlPanelParameter:'Last Actual Month', ControlPanelMeasure:'Value'] = S:
		ATTRS('Period', ATTRS('Period', DB('Control Panel', !Version, 'Starting Forecast Period', 'Value'), 'Prior Period'), 'Abbreviation');
# Converts Last Actual Monthinto a number value
[ ControlPanelParameter:'Last Actual Month', ControlPanelMeasure:'Numeric Value'] = N:
	NUMBR(ATTRS('Period',DB( 'Control Panel', !Version, !Control Panel Parameter, 'Value' ),'Numeric Month'));
# Description for Setting the Last Actual Month
[ ControlPanelParameter:'Last Actual Month', ControlPanelMeasure:'Description' ] = S: '';

# Prevent Last Actual Year from being calculated for the version called Actuals
[ Version:'Actuals', ControlPanelParameter:'Last Actual Year', ControlPanelMeasure:'Value'] = S: '';
# Calculates Last Actual Year
[ControlPanelParameter:'Last Actual Year', ControlPanelMeasure:'Value'] = S: IF(
	DB('Control Panel', !Version, 'Last Actual Month', 'Value') @= 'Dec',
		ATTRS('Year', DB('Control Panel', !Version, 'Starting Forecast Year', 'Value'), 'Prior Year'),
		DB('Control Panel', !Version, 'Starting Forecast Year', 'Value') );
# Converts Last Actual Year into a number value
[ ControlPanelParameter:'Last Actual Year', ControlPanelMeasure:'Numeric Value'] = N:
	NUMBR( DB( 'Control Panel', !Version, !Control Panel Parameter, 'Value' ));
# Description for Setting the Last Actual Year
[ ControlPanelParameter:'Last Actual Year', ControlPanelMeasure:'Description' ] = S: '';




