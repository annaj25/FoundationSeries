Skipcheck;

[]=If(ELISANC('Version','All Reporting Versions',!Version) =1,STET,Continue);

[{'FTE', 'Home FTE'}] = C: IF ( ELLEV ( 'Period', !Period ) > 0,
     DB ( 'Employee Department', !Version, !Employee, !Department, ATTRS ( 'Period', !Period, 'Period End' ), 'FTE' ),
     Continue );

[{'FTE', 'Home FTE'}] = N: IF ( DayNo( DB( 'Control Panel', !Version, 'Starting Forecast Year', 'Value' ) | '-' |
              '01' | '-' | '01' ) >
            DayNo( !Period | '-' | '01' ), 0, Continue);

#region New Employee

[{'FTE', 'Home FTE'}] = N: IF (
	ELISANC('Employee', 'New', !Employee) > 0 &
	DB ( 'Employee Forecast', !Version, !Department, !Employee, 'Start Date (TM1)' ) <= 0 ,
	0, Continue );

[{'FTE', 'Home FTE'}] = N: IF (
	ELISANC('Employee', 'New', !Employee) > 0 &
     DB ( '}Calendar', SUBST(!Period,1,4), 'M' | SUBST(!Period,6,2), 'TM1 End Date' ) <
		DB ( 'Employee Forecast', !Version, !Department, !Employee, 'Start Date (TM1)' ) ,
     0, Continue );

[{'FTE', 'Home FTE'}] = N: IF (
	ELISANC('Employee', 'New', !Employee) > 0 &
     DB ( 'Employee Forecast', !Version, !Department, !Employee, 'End Date (TM1)' ) > 0 &
	DB ( '}Calendar', SUBST(!Period,1,4), 'M' | SUBST(!Period,6,2), 'TM1 Beg Date' ) >
	DB ( 'Employee Forecast', !Version, !Department, !Employee, 'End Date (TM1)' ),
     0,  Continue );


[{'FTE', 'Home FTE'}] = N: IF ( ELISANC('Employee', 'New', !Employee) > 0,
	DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation Percentage'),
	Continue );

#endregion

#region Current Employees
[{'FTE', 'Home FTE'}] = C: IF ( ELLEV ( 'Period', !Period ) > 0,
     DB ( 'Employee Department', !Version, !Employee, !Department, ATTRS ( 'Period', !Period, 'Period End' ), 'FTE' ),
     Continue );

[{'FTE', 'Home FTE'}] = N: IF ( DB ( 'Employee Forecast', !Version, DB('}Employee Information',!Version, !Employee, 'Home Department'), !Employee, 'Start Date (TM1)' ) <= 0 , 0, Continue );

[{'FTE', 'Home FTE'}] = N: IF (
     DB ( '}Calendar', SUBST(!Period,1,4), 'M' | SUBST(!Period,6,2), 'TM1 End Date' ) <
	DB ( 'Employee Forecast', !Version, DB('}Employee Information',!Version, !Employee, 'Home Department'), !Employee, 'Start Date (TM1)' ) ,
     0,
     Continue );

[{'FTE' , 'Home FTE'}] = N: IF (
     DB ( 'Employee Forecast', !Version, DB('}Employee Information',!Version, !Employee, 'Home Department'), !Employee, 'End Date (TM1)' ) > 0 &
	(DB ( '}Calendar', SUBST(!Period,1,4), 'M' | SUBST(!Period,6,2), 'TM1 Beg Date' ) >
	DB ( 'Employee Forecast', !Version, DB('}Employee Information',!Version, !Employee, 'Home Department'), !Employee, 'End Date (TM1)' )),
     0,
     Continue );

['Home FTE'] = N: If (!Department @<>  DB('}Employee Information',!Version, !Employee, 'Home Department'),0,1);

['FTE'] = N: IF (
       DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation Percentage') <> 0  &
	DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation End (TM1)') <> 0 &
	DAYNO(SUBST(DATE(DB ( '}Calendar', SUBST(!Period,1,4), 'M' | SUBST(!Period,6,2), 'TM1 Beg Date' ),1),1,7) | '-01') >=
	DAYNO(SUBST(DATE(DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation End (TM1)'),1),1,7) | '-01') ,
     0,
     Continue );

['FTE'] = N: IF (
       DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation Percentage') <> 0  &
	DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation Start (TM1)') <> 0 &
	DAYNO(SUBST(DATE(DB ( '}Calendar', SUBST(!Period,1,4), 'M' | SUBST(!Period,6,2), 'TM1 Beg Date' ),1),1,7) | '-01') <
	DAYNO(SUBST(DATE(DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation Start (TM1)'),1),1,7) | '-01') ,
     0,
     Continue );

['FTE'] = N: DB('Employee Allocation', !Version, !Employee, !Department, 'Allocation Percentage');
#EndRegion

feeders;

['FTE'] => DB('Labor Forecast', !Version, 'local', !Employee, !Department, 'FTE', !Period, 'Value'),
	DB('Labor Forecast', !Version, 'local', !Employee, !Department, 'Home FTE', !Period, 'Home Department');

['Home FTE'] => DB('Labor Forecast', !Version, 'local',  !Employee, !Department, 'Home FTE', !Period, 'Home Department');
